plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    // Add protobuf plugin
    id "com.google.protobuf" version "0.8.12"
}

group = 'grpc.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// Add sourceSets for IDLE proto files recognition
sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
            srcDir 'src/main/protocolbuffers'
            include '**/*.protodevel'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
    test {
        proto {
            srcDir 'src/test/protocolbuffers'
        }
    }
}

// Add protobuf task for compile .proto files
protobuf {
    protoc {
        artifact ='com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    // Add grpc-spring-boot-starter and protobuf compile
    compile 'io.github.lognet:grpc-spring-boot-starter:3.3.0'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.10.0'

    implementation 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
